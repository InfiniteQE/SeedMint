0  BEGIN PGM BLOCK OBEN MM 
1  BLK FORM 0.1 Z X-40 Y+0 Z-30
2  BLK FORM 0.2  X+40  Y+80  Z+0
3  ;
4  * - schieber nut
5  TOOL CALL 1012 Z S7000 F4000
6  M3
7  ;
8  L  Y-10  Z+10
9  ;
10 CYCL DEF 14.0 KONTUR
11 CYCL DEF 14.1 KONTURLABEL1
12 CYCL DEF 275 KONTURNUT WIRBELFR. ~
    Q215=+0    ;BEARBEITUNGS-UMFANG ~
    Q219=+30   ;NUTBREITE ~
    Q368=+0.2  ;AUFMASS SEITE ~
    Q436=+1    ;ZUST. PRO UMLAUF ~
    Q207= AUTO ;VORSCHUB FRAESEN ~
    Q351=+1    ;FRAESART ~
    Q201=-3.05 ;TIEFE ~
    Q202=+10   ;ZUSTELL-TIEFE ~
    Q206=+1500 ;VORSCHUB TIEFENZ. ~
    Q338=+10   ;ZUST. SCHLICHTEN ~
    Q385=+1000 ;VORSCHUB SCHLICHTEN ~
    Q200=+2    ;SICHERHEITS-ABST. ~
    Q203=+0    ;KOOR. OBERFLAECHE ~
    Q204=+10   ;2. SICHERHEITS-ABST. ~
    Q366=+0    ;EINTAUCHEN ~
    Q369=+0    ;AUFMASS TIEFE ~
    Q439=+0    ;BEZUG VORSCHUB
13 M99
14 ;
15 LBL 1
16 L  X+0  Y-10 R0 F AUTO
17 L  Y+90 R0 F AUTO
18 LBL 0
19 ;
20 L  Z+100 R0 FMAX M9 M50 M5
21 ;
22 L  X+0  Y+35 R0 FMAX
23 L  Z-2.5 R0 F AUTO
24 L  Z-4.1 F50
25 L  Z-2 R0 F200
26 L  Z+100 R0 FMAX
27 * - gewinde
28 ;
29 TOOL CALL 3006 Z S4000 F800
30 M3 M46 M8
31 CYCL DEF 200 BOHREN ~
    Q200=+2    ;SICHERHEITS-ABST. ~
    Q201=-2.3  ;TIEFE ~
    Q206=+150  ;VORSCHUB TIEFENZ. ~
    Q202=+1    ;ZUSTELL-TIEFE ~
    Q210=+0    ;VERWEILZEIT OBEN ~
    Q203=+0    ;KOOR. OBERFLAECHE ~
    Q204=+20   ;2. SICHERHEITS-ABST. ~
    Q211=+0    ;VERWEILZEIT UNTEN ~
    Q395=+0    ;BEZUG TIEFE
32 CALL LBL 2
33 ;
34 TOOL CALL 2033 Z S4000 F150
35 CYCL DEF 200 BOHREN ~
    Q200=+2    ;SICHERHEITS-ABST. ~
    Q201=-15   ;TIEFE ~
    Q206=+150  ;VORSCHUB TIEFENZ. ~
    Q202=+5    ;ZUSTELL-TIEFE ~
    Q210=+0    ;VERWEILZEIT OBEN ~
    Q203=+0    ;KOOR. OBERFLAECHE ~
    Q204=+50   ;2. SICHERHEITS-ABST. ~
    Q211=+0    ;VERWEILZEIT UNTEN ~
    Q395=+0    ;BEZUG TIEFE
36 CALL LBL 2
37 ;
38 TOOL CALL 4004 Z S200
39 CYCL DEF 207 GEW.-BOHREN GS ~
    Q200=+2    ;SICHERHEITS-ABST. ~
    Q201=-10   ;GEWINDETIEFE ~
    Q239=+0.7  ;GEWINDESTEIGUNG ~
    Q203=+0    ;KOOR. OBERFLAECHE ~
    Q204=+50   ;2. SICHERHEITS-ABST.
40 CALL LBL 2
41 ;
42 LBL 2
43 CYCL DEF 221 MUSTER LINIEN ~
    Q225=-33   ;STARTPUNKT 1. ACHSE ~
    Q226=+76   ;STARTPUNKT 2. ACHSE ~
    Q237=+66   ;ABSTAND 1. ACHSE ~
    Q238=-16   ;ABSTAND 2. ACHSE ~
    Q242=+2    ;ANZAHL SPALTEN ~
    Q243=+2    ;ANZAHL ZEILEN ~
    Q224=+0    ;DREHLAGE ~
    Q200=+2    ;SICHERHEITS-ABST. ~
    Q203=+0    ;KOOR. OBERFLAECHE ~
    Q204=+50   ;2. SICHERHEITS-ABST. ~
    Q301=+1    ;FAHREN AUF S. HOEHE
44 ;
45 L  X-17.5  Y+17.5 R0 M99
46 L  X+17.5 R0 M99
47 LBL 0
48 ;
49 TOOL CALL 3006 Z S2500 F150
50 M3 M46 M8
51 CYCL DEF 200 BOHREN ~
    Q200=+2    ;SICHERHEITS-ABST. ~
    Q201=-1.6  ;TIEFE ~
    Q206=+150  ;VORSCHUB TIEFENZ. ~
    Q202=+5    ;ZUSTELL-TIEFE ~
    Q210=+0    ;VERWEILZEIT OBEN ~
    Q203=-3    ;KOOR. OBERFLAECHE ~
    Q204=+50   ;2. SICHERHEITS-ABST. ~
    Q211=+0    ;VERWEILZEIT UNTEN ~
    Q395=+0    ;BEZUG TIEFE
52 L  X+0  Y+17.5 R0 FMAX M99
53 TOOL CALL 2030 Z S3500 F150
54 CYCL DEF 200 BOHREN ~
    Q200=+2    ;SICHERHEITS-ABST. ~
    Q201=-10   ;TIEFE ~
    Q206=+150  ;VORSCHUB TIEFENZ. ~
    Q202=+5    ;ZUSTELL-TIEFE ~
    Q210=+0    ;VERWEILZEIT OBEN ~
    Q203=-3    ;KOOR. OBERFLAECHE ~
    Q204=+50   ;2. SICHERHEITS-ABST. ~
    Q211=+0    ;VERWEILZEIT UNTEN ~
    Q395=+1    ;BEZUG TIEFE
55 L  X+0  Y+17.5 R0 FMAX M99
56 END PGM BLOCK OBEN MM 
